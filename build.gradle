allprojects {
    group = 'org.fcnabc.autoppt'
    version = '0.1.0'

    repositories {
        mavenCentral()
    }
}

subprojects {
    plugins.withId('java') {
        configurations {
            mockitoAgent
        }

        dependencies {
            mockitoAgent('org.mockito:mockito-core:5.21.0') {
                transitive = false
            }
        }

        java {
            toolchain {
                languageVersion = JavaLanguageVersion.of(25)
            }
        }

        test {
            useJUnitPlatform()
            jvmArgs("--add-opens", "java.base/java.lang=ALL-UNNAMED")
            jvmArgs("-javaagent:${configurations.mockitoAgent.asPath}")
            
            testLogging {
                events = ["standardOut", "standardError"]
                exceptionFormat = "full"
            }
        }

        sourceSets {
            main {
                resources {
                    srcDir "${rootProject.projectDir}/config/main-resources"
                }
            }
            test {
                resources {
                    srcDir "${rootProject.projectDir}/config/test-resources"
                }
            }
        }
    }
}
